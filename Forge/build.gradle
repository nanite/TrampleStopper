plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '6.0.+'
    id 'net.darkhax.curseforgegradle' version '1.0.14'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

archivesBaseName = "${project.property("mod_name")}-${project.name}"

Project commonProject = parent.project("Common")

def mixinEnabled = project.property("mixinsEnabled") == "true"
def forgeDataGen = project.property("forgeDataGen") == "true"

if(mixinEnabled) {
    println("Mixins Enabled!")
    mixin {
        add commonProject.sourceSets.main, "${mod_id}.refmap.json"
        config("${mod_id}.mixins.json")
    }
}

minecraft {
    mappings channel: 'official', version: minecraft_version

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${parent.name}.${project.name}.main"
            taskName 'Client'
            if(mixinEnabled) {
                property 'mixin.env.remapRefMap', 'true'
                property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            }
            mods {
                modClientRun {
                    source sourceSets.main
                    source commonProject.sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${parent.name}.${project.name}.main"
            taskName 'Server'
            if(mixinEnabled) {
                property 'mixin.env.remapRefMap', 'true'
                property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            }
            mods {
                modServerRun {
                    source sourceSets.main
                    source commonProject.sourceSets.main
                }
            }
        }

        if(forgeDataGen) {
            data {
                workingDirectory project.file('run')
                ideaModule "${rootProject.name}.${project.name}.main"
                args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
                taskName 'Data'
                property 'mixin.env.remapRefMap', 'true'
                property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
                mods {
                    modDataRun {
                        source sourceSets.main
                        source commonProject.sourceSets.main
                    }
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    compileOnly commonProject
    if(mixinEnabled) {
        annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    }
    implementation fg.deobf("com.unrealdinnerbone:Trenzalore-Forge:${trenzalore_version}+mc${minecraft_version}")


}

tasks.withType(JavaCompile) {
    source(commonProject.sourceSets.main.allSource)
}

processResources {
    from commonProject.sourceSets.main.resources
}

jar.finalizedBy('reobfJar')

task curseforgePublish(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {

    apiToken = ENV.CURSE_TOKEN
    def mainFile = upload(findProperty('curse_id'), jar)
    mainFile.changelog = file("../CHANGELOG.md")
    mainFile.releaseType = "beta"
    mainFile.changelogType = "markdown"
    mainFile.addRequirement("trenzalore")
    mainFile.addJavaVersion("Java 17")
}



publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
        }
    }

    repositories {
        if (ENV.NANITE_TOKEN) {
            maven {
                url "https://maven.nanite.dev/${SNAPSHOT ? "snapshots" : "releases"}"
                credentials {
                    username = "nanite"
                    password = "${ENV.NANITE_TOKEN}"
                }
            }
        }
    }
}